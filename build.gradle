plugins {
	id "java"
	id "application"
	id "idea"
	id "org.sonarqube" version "3.3"
	id "jacoco"
	id "com.diffplug.spotless" version "6.1.0"
}

repositories {
	mavenCentral()
}

sonarqube {
	properties {
		property "sonar.projectKey", "alphagov_di-ipv-cri-uk-passport-back"
		property "sonar.organization", "alphagov"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

spotless {
	java {
		target "**/src/**/*.java"
		googleJavaFormat("1.13.0").aosp()
		importOrder "", "javax", "java", "\\#"
		endWithNewline()
	}
	groovyGradle {
		target '**/*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

ext {
	dependencyVersions = [
		awsJavaSdkDynamodb:'1.12.146',
		awsJavaSdkSqs:'1.12.197',
		awsLambdaJavaCore:'1.2.1',
		awsLambdaJavaEvents:'3.11.0',
		awsLambdaJavaLog4j2:'1.5.1',
		dynamodbEnhanced:'2.17.116',
		gson:'2.8.9',
		jackson:'2.13.1',
		log4j:'2.17.1',
		nimbusJoseJwt:'9.16',
		nimbusdsOauth2OidcSdk:'9.22.1',
		powertoolsParameters:'1.9.0',
		slf4j:'1.7.33'
	]
}

subprojects {

	configurations {
		loggingImplementation
		loggingRunTimeOnly
	}

	dependencies {
		loggingImplementation "org.slf4j:slf4j-api:$rootProject.ext.dependencyVersions.slf4j"

		loggingRunTimeOnly "org.apache.logging.log4j:log4j-api:$rootProject.ext.dependencyVersions.log4j",
				"org.apache.logging.log4j:log4j-core:$rootProject.ext.dependencyVersions.log4j",
				"org.apache.logging.log4j:log4j-layout-template-json:$rootProject.ext.dependencyVersions.log4j",
				"org.apache.logging.log4j:log4j-slf4j18-impl:$rootProject.ext.dependencyVersions.log4j",
				"com.amazonaws:aws-lambda-java-log4j2:$rootProject.ext.dependencyVersions.awsLambdaJavaLog4j2",
				"com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$rootProject.ext.dependencyVersions.jackson"
	}

	task allDeps(type: DependencyReportTask) {}
}
